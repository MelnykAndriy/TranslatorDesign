

# parser 

<signal-program> --> <program>

<program> --> PROCEDURE <procedure-identifier> <parameters-list> ;
                <block> ;

<parameters-list> --> ( <variable-identifier> <identifiers-list> ) | <empty>

<identifiers-list> --> , <variable-identifier> <identifiers-list> | <empty>

<block> --> <declarations> 
            BEGIN
                <statements-list>
            END

<declarations> --> <label-declarations>

<label-declarations> --> LABEL <unsigned-integer> <labels-list> ; |  <empty>

<labels-list> --> , <unsigned-integer> <labels-list> | <empty>

<statements-list> --> <statement> <statements-list> | <empty>

<statement> --> <unsigned-integer> : <statement>
                | GOTO <unsigned-integer> ;
                | RETURN ;
                | ;
                | ($ <assembly-insert-file-identifier> $)

<variable-identifier> --> <identifier>

<procedure-identifier> --> <identifier>

<assembly-insert-file-identifier> --> <identifier>

# lexer

<identifier> --> <letter><string>

<string> -->  <letter><string> 
            | <digit><string> 
            |  <empty>
            
<unsigned-integer> --> <digit> <digits-string>

<digits-string> --> <digit> <digits-string> | <empty>

<digit> --> [0-9]

<letter> --> [A-Z]